import io, util;

public final long DEFAULT_READ_BUF_SIZE = 4L << 10;

public class Reader
{
    public Reader(io.Readable r, long buf_size)
    {
        if (buf_size <= 0)
        {
            buf_size = DEFAULT_READ_BUF_SIZE;
        }

        this.r        = r;
        this.buf      = new char[buf_size];
        this.buf_data = this.buf[: 0];
    }

    public io.Readable readable()
    {
        return this.r;
    }

    public long buf_size()
    {
        return this.buf.size();
    }

    public long buf_data_size()
    {
        return this.buf_data.size();
    }

    //读取一个字符，若EOF则返回false，否则读取成功返回true
    public bool read_char(ref char c)
    {
        if (this.fill_buf() == 0)
        {
            return false;
        }
        c = this.buf_data[0];
        this.buf_data = this.buf_data[1 :];
        return true;
    }

    public long read(char[] b)
    {
        long dst_buf_size = b.size();
        if (dst_buf_size == 0) {
            throw(new ValueError("read参数为空buf"));
        }

        if (this.buf_data_size() == 0)
        {
            //buf没数据，对于大的数据请求直接读，小的则fill buf
            if (dst_buf_size >= this.buf_size())
            {
                return this.r.read(b);
            }
            if (this.fill_buf() == 0)
            {
                //读到末尾了
                return 0;
            }
        }

        //buf有数据，拷贝过去
        long copy_size = b.shallow_copy_from(this.buf_data);
        this.buf_data = this.buf_data[copy_size :];

        return copy_size;
    }

    //读取数据直到EOF或b被填满
    public long read_until_eof(char[] b)
    {
        long b_sz = b.size(), read_sz = 0;
        while (read_sz < b_sz)
        {
            long sz = this.read(b[read_sz :]);
            if (sz == 0)
            {
                break;
            }
            read_sz += sz;
        }
        return read_sz;
    }

    //读取数据直到EOF、或读到指定字符、或b被填满，如读到指定字符结束，则包含指定字符，即指定字符为返回长度指定的范围内的最后一个字符
    public long read_until_char(char[] b, char end_char)
    {
        long b_sz = b.size(), read_sz = 0;
        while (read_sz < b_sz)
        {
            if (this.fill_buf() == 0)
            {
                break;
            }
            var data_to_copy = this.buf_data;
            long idx = data_to_copy.index(end_char);
            if (idx >= 0)
            {
                data_to_copy = data_to_copy[: idx + 1];
            }
            long copy_size = b[read_sz :].shallow_copy_from(data_to_copy);
            this.buf_data = this.buf_data[copy_size :];
            read_sz += copy_size;
            if (b[read_sz - 1] == end_char)
            {
                break;
            }
        }
        return read_sz;
    }

    public String read_line()
    {
        util.Vector<String> sl = nil;
        for (;;)
        {
            if (this.fill_buf() == 0)
            {
                //读到末尾了
                if (sl === nil)
                {
                    return nil;
                }
                return "".join(sl.iter());
            }
            long idx = this.buf_data.index('\n');
            if (idx >= 0)
            {
                //找到换行符了
                long line_len = idx + 1;
                var s = new String(this.buf_data[: line_len]);
                this.buf_data = this.buf_data[line_len :];
                if (sl === nil)
                {
                    return s;
                }
                sl.add(s);
                return "".join(sl.iter());
            }
            //一行太长了还没读完，加入sl后继续读
            if (sl === nil)
            {
                sl = new util.Vector<String>(nil);
            }
            sl.add(new String(this.buf_data));
            this.buf_data = this.buf[: 0];
        }
    }

    long fill_buf()
    {
        long buf_data_size = this.buf_data_size();
        if (buf_data_size == 0)
        {
            //buf没有数据才需要fill，读取数据到buf中
            buf_data_size = this.r.read(this.buf);
            this.buf_data = this.buf[: buf_data_size];
        }
        return buf_data_size;
    }

    void drop_buf_data()
    {
        this.buf_data = this.buf[: 0];
    }

    io.Readable r;
    char[]      buf;
    char[]      buf_data;
}
