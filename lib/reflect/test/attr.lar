import testing, reflect;

class ForAttrTesting
{
    ForAttrTesting()
    {
        this.x = 123;
        this.y = "abc";
        this.z = new ValueError("test");

        this.a = 456;
        !<<
        this.b = "def"
        !>>
    }

    public int          x   `test_tag:"attr test" name:"x"`;
    public String       y;
    public Throwable    z   `name:"attr_z"`;

    long a;
    !<<
    b string
    !>>
}

void test_class_attr()
{
    var t = new ForAttrTesting();

    {
        var tester = []{
            void assert(bool cond, String desc)
            {
                testing.assert(cond, "Type‘ForAttrTesting’：%s".(desc));
            }
        };

        var tp = reflect.type_of(t);
        var ais = tp.attr_infos();
        tester.assert(ais.size() == 3, "属性数量不为3");

        var correct_names = new String[]{"x", "y", "z"};
        var correct_type_names = new String[]{"int", "String", "Throwable"};
        var correct_types = new reflect.Type[]{reflect.type_of(0), reflect.type_of(""), nil};
        var correct_tag_lists = new Pair<String, String>[][]{
            {
                "test_tag": "attr test",
                "name":     "x",
            },
            {
            },
            {
                "name": "attr_z",
            },
        };

        for (int i = 0; i < 3; ++ i)
        {
            var ai = ais[i];

            var correct_name = correct_names[i];
            var an = ai.name();
            tester.assert(an.eq(correct_name), "第%d个属性名应为‘%s’，而不是‘%s’".(i + 1, correct_name, an));

            var attr_tester = []{
                void assert(bool cond, String desc)
                {
                    tester.assert(cond, "属性‘%s’：%s".(an, desc));
                }
            };

            attr_tester.assert(ai.cls() === tp, "cls返回的Type不是其所属的Type对象");

            var correct_tn = correct_type_names[i];
            var atn = ai.type_name();
            attr_tester.assert(atn.eq(correct_tn), "类型名应为‘%s’，而不是‘%s’".(correct_tn, atn));

            var correct_tp = correct_types[i];
            var atp = ai.type();
            attr_tester.assert(atp === correct_tp, "类型错误");

            var correct_tl = correct_tag_lists[i];
            var atl = ai.tags();
            attr_tester.assert(atl.eq(correct_tl), "tag错误，应为‘%r’，而不是‘%r’".(correct_tl, atl));
        }
    }

    {
        var tester = []{
            void assert(bool cond, String desc)
            {
                testing.assert(cond, "‘ForAttrTesting’实例的Value：%s".(desc));
            }
        };

        var v = reflect.value_of(t);
        var attr_infos = v.type().attr_infos();
        var attrs = v.attrs();
        tester.assert(attr_infos.size() == 3 && attrs.size() == 3, "属性数量不为3");

        int idx = -1;
        var attr_tester = []{
            void assert(bool cond, String desc)
            {
                tester.assert(cond, "属性‘%s’：%s".(attr_infos[idx].name(), desc));
            }
        };

        var old_values = new Any[]{t.x, t.y, t.z};
        var invalid_new_value_lists = new Any[][]{
            {"123", 0xabc, new int[1]},
            {nil, nil, 0.1},
        };
        var valid_new_value_lists = new Any[][]{
            {456, "def", new KeyError("test")},
            {0, (String)nil, nil},
        };

        for (idx = 0; idx < 3; ++ idx)
        {
            foreach (var invalid_new_values: invalid_new_value_lists.iter())
            {
                test_ref_value(attr_tester, attrs[idx], old_values[idx], invalid_new_values[idx], false);
            }
            var old_value = old_values[idx];
            foreach (var valid_new_values: valid_new_value_lists.iter())
            {
                test_ref_value(attr_tester, attrs[idx], old_value, valid_new_values[idx], true);
                old_value = valid_new_values[idx];
            }
        }
    }
}

void test_closure_attr()
{
    var t = []{};
    var v = reflect.value_of(t);
    var attr_infos = v.type().attr_infos();
    var attrs = v.attrs();
    tester.assert(attr_infos.size() == 0 && attrs.size() == 0, "闭包对象的属性数量不为0");
}

void test_attr()
{
    test_class_attr();
    test_closure_attr();
}
