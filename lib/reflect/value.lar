interface Ref
{
    String type_name();
    Any get();
    bool can_set(Any a);
    void set(Any a);
}

public class Value
{
    public String str()
    {
        if (this.is_ref())
        {
            return this.repr();
        }
        return "%s".(this.v);
    }

    public String repr()
    {
        Any a;
        String tp;
        if (this.is_ref())
        {
            a = this.r.get();
            tp = "ref ".concat(this.r.type_name());
        }
        else
        {
            a = this.v;
            tp = "%T".(a);
        }
        return "<reflect.Value of <%s> %s>".(tp, a);
    }

    //内部使用，不管是普通值还是ref指针都直接返回，用于lar_reflect_intf等更底层的接口
    GoAny get_raw()
    {
        return this.v;
    }

    public Any get()
    {
        return this.r.get() if this.is_ref() else this.v;
    }

    public bool is_ref()
    {
        return this.r !== nil;
    }

    public bool can_set(Any a)
    {
        return this.is_ref() && this.r.can_set(a);
    }

    public void set(Any a)
    {
        this.r.set(a) if this.is_ref() else throw(new Error{"Value的值不是ref类型，不能set"});
    }

    GoAny v; //具体的值，如果是ref，则为一个指针
    Ref   r; //当为ref时，对其操作的接口，普通值的这个字段为nil
}

public Value value_of(Any v)
{
    return new Value{
        v: v,
    };
}

public Value value_of_ref<T>(ref T t)
{
    GoAny v;
    !<<
    v = l_t;
    !>>
    String name_of_T = type_name<T>();
    return new Value{
        v: v,
        r: []{
            String type_name()
            {
                return name_of_T;
            }

            Any get()
            {
                return t;
            }

            bool can_set(Any a)
            {
                return try_assert_type<T>(a, ref _);
            }

            void set(Any a)
            {
                T av;
                if (try_assert_type(a, ref av))
                {
                    t = av;
                    return;
                }
                throw(new Error{"无法将类型‘%T’的值赋值给类型‘%s’".(a, name_of_T)});
            }
        },
    };
}

GoAny[] values_to_raw(Value[] vs)
{
    long sz = vs.size();
    var rs = new GoAny[sz];
    for (long i = 0; i < sz; ++ i)
    {
        rs[i] = vs[i].get_raw();
    }
    return rs;
}
