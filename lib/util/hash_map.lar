import debug;

class HashMapNode<K, V>
{
    K k;
    V v;

    HashMapNode<K, V> next;
}

public class HashMap<K, V>
{
    public HashMap(Iter<Pair<K, V>> data_iter)
    {
        this.tbl = new HashMapNode<K, V>[1 << 3]; //表大小为2的幂
        this.sz = 0;
        this.dirty = 0;

        if (data_iter !== nil)
        {
            this.update(data_iter);
        }
    }

    public String repr()
    {
        var sl = new String[this.size() + 2];
        sl[0] = "<%T {".(this);
        long i = 1;
        foreach (var p : this.iter())
        {
            if (i == 1)
            {
                sl[i] = "%r: %r".(p.first, p.second);
            }
            else
            {
                sl[i] = ", %r: %r".(p.first, p.second);
            }
            ++ i;
        }
        sl[i] = "}>";
        ++ i;
        return "".join(sl[: i].iter());
    }

    public void update(Iter<Pair<K, V>> data_iter)
    {
        foreach (var p : data_iter)
        {
            this.set(p.first, p.second);
        }
    }

    public long size()
    {
        return this.sz;
    }

    //查找k，返回找到的idx位置以及node，若node为nil则表示不存在，冲突解决方式：链表法
    long find_node(HashMapNode<K, V>[] tbl, K k, ref HashMapNode<K, V> node)
    {
        long idx = hash(k) & (tbl.size() - 1); //用位运算代替取余;

        for (node = tbl[idx]; node !== nil && !eq(node.k, k); node = node.next)
        {
        }

        return idx;
    }

    void rehash_if_necessary()
    {
        long tbl_size = this.tbl.size();
        long new_size;
        if (tbl_size > 1L << 10 && this.sz < tbl_size * 15 / 100)
        {
            //表比较大且装载率太低，缩容，缩小一半
            new_size = tbl_size >> 1;
        }
        else if (this.sz > tbl_size * 7 / 10)
        {
            //装载率过高，扩容，若表比较大则扩大2倍，否则扩大4倍
            new_size = tbl_size << 1;
            if (new_size < 1L << 16)
            {
                new_size <<= 1;
            }
        }
        else
        {
            //装载率正常，不做调整
            return;
        }
        debug.assert(new_size > 0, "bug");

        //创建新表并迁移旧表数据，这个过程将来可以改成渐进式
        var new_tbl = new HashMapNode<K, V>[new_size];
        for (var i = this.tbl.size() - 1; i >= 0; -- i)
        {
            for (var node = this.tbl[i]; node !== nil; node = node.next)
            {
                HashMapNode<K, V> new_node;
                long idx = this.find_node(new_tbl, node.k, ref new_node);
                debug.assert(new_node === nil, "bug"); //在新表插入node肯定找不到的
                new_node = new HashMapNode<K, V>();
                new_node.k = node.k;
                new_node.v = node.v;
                new_node.next = new_tbl[idx];
                new_tbl[idx] = new_node;
            }
        }
        this.tbl = new_tbl;
        ++ this.dirty;
    }

    V get_or_pop(K k, bool need_pop)
    {
        HashMapNode<K, V> node;
        long idx = this.find_node(this.tbl, k, ref node);
        if (node === nil)
        {
            throw(new KeyError("%r".(k)));
        }
        if (need_pop)
        {
            if (this.tbl[idx] === node)
            {
                //第一个节点就是，直接删掉
                this.tbl[idx] = node.next;
            }
            else
            {
                //在链表中找到节点的前驱，通过修改其next属性来删掉节点
                for (var nd = this.tbl[idx]; ; nd = nd.next)
                {
                    debug.assert(nd !== nil, "bug");
                    if (nd.next === node)
                    {
                        nd.next = node.next;
                        break;
                    }
                }
            }
            -- this.sz;
            ++ this.dirty;
            this.rehash_if_necessary();
        }
        return node.v;
    }

    public V get(K k)
    {
        return this.get_or_pop(k, false);
    }

    public V pop(K k)
    {
        return this.get_or_pop(k, true);
    }

    public void set(K k, V v)
    {
        this.rehash_if_necessary();

        HashMapNode<K, V> node;
        long idx = this.find_node(this.tbl, k, ref node);
        if (node === nil)
        {
            //新增节点，插到链表头部
            node = new HashMapNode<K, V>();
            node.k = k;
            node.v = v;
            node.next = this.tbl[idx];
            this.tbl[idx] = node;
            ++ this.sz;
            ++ this.dirty;
        }
        else {
            node.v = v;
        }
    }

    public HashMapIter<K, V> iter()
    {
        return new HashMapIter<K, V>(this, 0, this.tbl[0]);
    }

    public HashMapIter<K, V> find(K k)
    {
        HashMapNode<K, V> node;
        long idx = this.find_node(this.tbl, k, ref node);
        if (node === nil)
        {
            return nil;
        }
        return new HashMapIter<K, V>(this, idx, node);
    }

    public bool contains_key(K k)
    {
        HashMapNode<K, V> node;
        this.find_node(this.tbl, k, ref node);
        return node !== nil;
    }

    HashMapNode<K, V>[] tbl;
    long sz;
    long dirty;
}

public class HashMapIter<K, V>
{
    HashMapIter(HashMap<K, V> hm, long idx, HashMapNode<K, V> node)
    {
        this.hm = hm;
        this.idx = idx;
        this.nd = node;
        this.dirty = hm.dirty;
        this.skip_nil();
    }

    void skip_nil()
    {
        var tbl = this.hm.tbl;
        long tbl_size = tbl.size();
        long idx = this.idx;
        var node = this.nd;

        //若node为nil，则向后找最近一个合法的数据
        while (node === nil)
        {
            ++ idx;
            if (idx >= tbl_size)
            {
                break;
            }
            node = tbl[idx];
        }
        this.idx = idx;
        this.nd = node;
    }

    void check_dirty()
    {
        if (this.hm.dirty != this.dirty)
        {
            throw(new IterDirty());
        }
    }

    public bool _after_end()
    {
        return this.idx >= this.hm.tbl.size();
    }

    public bool after_end()
    {
        this.check_dirty();
        return this._after_end();
    }

    HashMapNode<K, V> node()
    {
        this.check_dirty();
        if (this._after_end())
        {
            throw(new IterOutOfRange());
        }
        debug.assert(this.nd !== nil, "bug");
        return this.nd;
    }

    public Pair<K, V> get()
    {
        var nd = this.node();
        return make_pair(nd.k, nd.v);
    }

    public K key()
    {
        return this.node().k;
    }

    public V value()
    {
        return this.node().v;
    }

    public void set(V v)
    {
        this.node().v = v;
    }

    public void inc()
    {
        this.check_dirty();
        if (this._after_end())
        {
            throw(new IterOutOfRange());
        }
        debug.assert(this.nd !== nil, "bug");
        this.nd = this.nd.next;
        this.skip_nil();
    }

    HashMap<K, V> hm;
    long idx;
    HashMapNode<K, V> nd;
    long dirty;
}
