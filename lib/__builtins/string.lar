//String类的定义

!<<

import (
    "strings"
    "strconv"
    "fmt"
)

!>>

public class String
{
    public String(char[] arr)
    {
        !<<
        this.s = string(l_arr.arr)
        !>>
        this.h = 0;
    }

    public String str()
    {
        return this;
    }

    public long len()
    {
        !<<
        return int64(len(this.s))
        !>>
    }

    public char char_at(long idx)
    {
        !<<
        return this.s[l_idx]
        !>>
    }

    public char[] to_char_array()
    {
        !<<
        return &lar_arr_uint8_1{arr: []uint8(this.s)}
        !>>
    }

    public String repr()
    {
        !<<
        return &lar_cls_@<<String>>{
            s: fmt.Sprintf("%q", this.s),
        }
        !>>
    }

    public int cmp(String other)
    {
        !<<
        if this.s < l_other.s {
            return int32(-1)
        }
        if this.s > l_other.s {
            return int32(1)
        }
        return int32(0)
        !>>
    }

    public bool eq(String other)
    {
        !<<
        return this.s == l_other.s;
        !>>
    }

    public long hash()
    {
        if (this.h == 0)
        {
            ulong h;
            !<<
            s := this.s
            sl := len(s)
            for i := 0; i < sl; i ++ {
                l_h = (l_h + uint64(s[i])) * 1000003
            }
            !>>
            if (h == 0)
            {
                h = 1;
            }
            this.h = (long)h;
        }
        return this.h;
    }

    public long index(String s)
    {
        !<<
        return int64(strings.Index(this.s, l_s.s))
        !>>
    }

    public String concat(String s)
    {
        !<<
        return &lar_cls_@<<String>>{
            s: this.s + l_s.s,
        }
        !>>
    }

    public bool startswith(String s)
    {
        !<<
        return strings.HasPrefix(this.s, l_s.s);
        !>>
    }

    public bool endswith(String s)
    {
        !<<
        return strings.HasSuffix(this.s, l_s.s);
        !>>
    }

    public String join(Iter<String> si)
    {
        !<<
        sl := make([]string, 0, 16)
        for ; !l_si.lar_method_after_end(); l_si.lar_method_inc() {
            sl = append(sl, lar_str_to_go_str(l_si.lar_method_get()))
        }
        return lar_str_from_go_str(strings.Join(sl, this.s))
        !>>
    }

    public String[] split(String sep, long count)
    {
        !<<
        var sl []string
        if l_sep == nil {
            sl = strings.Fields(this.s)
        } else if len(l_sep.s) == 0 {
            s_len := len(this.s)
            sl = make([]string, 0, s_len)
            for i := 0; i < s_len; i ++ {
                sl = append(sl, this.s[i : i + 1])
            }
        } else {
            sl = strings.SplitN(this.s, l_sep.s, int(l_count))
        }
        a := make([]*lar_cls_@<<String>>, 0, len(sl))
        for _, s := range sl {
            a = append(a, lar_str_from_go_str(s))
        }
        return &lar_arr_lar_cls_@<<String>>_1{
            arr: a,
        }
        !>>
    }

    public String sub_str(long begin, long end)
    {
        throw_on_range_error(this.len(), begin, end);
        !<<
        return lar_str_from_go_str(this.s[l_begin : l_end])
        !>>
    }

    public String repeat(long count)
    {
        long s_len = this.len();
        if (count < 0 || (count > 0 && s_len * count / count != s_len))
        {
            throw(new ValueError("String.repeat参数错误，count=%d，字符串长度=%d".(count, s_len)));
        }
        !<<
        return lar_str_from_go_str(strings.Repeat(this.s, int(l_count)));
        !>>
    }

    public bool parse_bool()
    {
        bool r, ok;
        !<<
        var err error
        l_r, err = strconv.ParseBool(this.s)
        l_ok = err == nil
        !>>
        if (!ok)
        {
            throw(new ValueError("无效的bool字面量：%r".(this)));
        }
        return r;
    }

    public long parse_long(int base)
    {
        long r;
        bool ok;
        !<<
        var err error
        l_r, err = strconv.ParseInt(this.s, int(l_base), 64)
        l_ok = err == nil
        !>>
        if (!ok)
        {
            String err_info;
            if (base == 0)
            {
                err_info = "无效的long字面量：%r".(this);
            }
            else
            {
                err_info = "无效的%d进制long字面量：%r".(base, this);
            }
            throw(new ValueError(err_info));
        }
        return r;
    }

    public ulong parse_ulong(int base)
    {
        ulong r;
        bool ok;
        !<<
        var err error
        l_r, err = strconv.ParseUint(this.s, int(l_base), 64)
        l_ok = err == nil
        !>>
        if (!ok)
        {
            String err_info;
            if (base == 0)
            {
                err_info = "无效的ulong字面量：%r".(this);
            }
            else
            {
                err_info = "无效的%d进制ulong字面量：%r".(base, this);
            }
            throw(new ValueError(err_info));
        }
        return r;
    }

    public double parse_double()
    {
        double r;
        bool ok;
        !<<
        var err error
        l_r, err = strconv.ParseFloat(this.s, 64)
        l_ok = err == nil
        !>>
        if (!ok)
        {
            throw(new ValueError("无效的double字面量：%r".(this)));
        }
        return r;
    }

    !<<
    s string;
    !>>
    long h; //缓存的hash值，为0表示还没有计算
}
