import time, net, math/rand;

void log(String msg)
{
    defer
    {
        catch<Throwable>();
    }

    println("[%s] %s".(time.format_time("2006-01-02 15:04:05", time.time()), msg));
}

class Client
{
    net.TcpConn conn;
    char[] result;

    Client(net.TcpConn conn)
    {
        this.conn = conn;

        this.result = new char[4];
        for (long i = 0; i < 4; )
        {
            char c = '0' + (char)rand.rand_n(10);
            if (this.result[: i].index(c) < 0)
            {
                this.result[i] = c;
                ++ i;
            }
        }
        this.client_log("start with result [%s]".(new String(this.result)));
    }

    void client_log(String s)
    {
        log("client [%s]: %s".(this.conn.remote_addr(), s));
    }

    bool is_valid_input(char[] s)
    {
        if (s.size() != 6)
        {
            return false;
        }
        for (long i = 0; i < 4; ++ i)
        {
            char c = s[i];
            if (c < '0' || c > '9' || s[: i].index(c) >= 0)
            {
                return false;
            }
        }
        return true;
    }

    void send_str(String s)
    {
        this.conn.write(s.to_char_array());
    }
    void send_line(String s)
    {
        this.send_str(s.concat("\r\n"));
    }

    void _run()
    {
        char[] recv_buf = new char[1000];
        int[] rsp = new int[2];
        while (true)
        {
            this.send_str("Guess a number, 4 different digits:");

            long recved_size = this.conn.read(recv_buf);
            if (recved_size == 0)
            {
                this.client_log("client over");
                return;
            }

            char[] input = recv_buf[: recved_size];
            long input_size = input.size();

            if (!(input_size >= 2 && input[input_size - 2] == '\r' && input[input_size - 1] == '\n'))
            {
                throw("proto error");
            }
            if (input_size == 2)
            {
                continue;
            }

            if (!this.is_valid_input(input))
            {
                this.send_line("Invalid input");
                continue;
            }

            rsp[0] = 0;
            rsp[1] = 0;
            for (long i = 0; i < 4; ++ i)
            {
                for (long j = 0; j < 4; ++ j)
                {
                    if (this.result[i] == recv_buf[j])
                    {
                        ++ rsp[0 if i == j else 1];
                    }
                }
            }
            if (rsp[0] == 4)
            {
                this.send_line("You got it");
                this.client_log("player win");
                return;
            }
            this.send_line("%dA%dB".(rsp[0], rsp[1]));
        }
    }

    public void run()
    {
        defer
        {
            catch<Throwable>();
        }
        defer this.conn.close();

        var c = call_and_catch<Throwable>([]{
            public void call()
            {
                this._run();
            }
        });
        if (c !== nil)
        {
            this.client_log("client ends with exception [%s], traceback[%s]".(c.throwed().str(), c.traceback()));
        }
    }
}

public void main()
{
    var listener = new net.TcpListener(":9999");
    defer listener.close();

    log("game start!");
    for (;;)
    {
        var c = call_and_catch<Throwable>([]{
            public void call()
            {
                var conn = listener.accept();
                log("accept success, new client [%s]".(conn.remote_addr()));

                start_fiber(new Client(conn));
            }
        });
        if (c !== nil)
        {
            log("accept new client failed [%s][%s]".(c.throwed().str(), c.traceback()));
        }
    }
}
